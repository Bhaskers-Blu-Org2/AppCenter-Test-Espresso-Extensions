
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.2'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'OldTargetApi', 'UnusedResources'
    }
}

version = '1.0'

repositories {
    mavenLocal()
    jcenter()
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

task sourcesJar(type: Jar) {
    // There is no allSources in the androidPlugin :-(
    from 'src'
    classifier = 'sources'
}

// pom file name
ext {
    pomFilePath = "${project.projectDir}/build/publications/androidLib/pom-default.xml"
    pomFile = file(pomFilePath)
}
// add configuration for pom signing
configurations {
    pom
}

def aar = file("build/outputs/aar/appcenter-espresso-test-extension-release.aar")

artifacts {
    archives sourcesJar
    archives aar
    pom pomFile
}

// sign all artifacts
task signArtifacts (type : Sign, dependsOn: sourcesJar) {
    sign configurations.archives
}
signArtifacts.dependsOn {
    tasks.findAll { task -> task.name.startsWith('generatePom')}
}

task preparePublication (dependsOn : signArtifacts)
publish.dependsOn { preparePublication }
publishToMavenLocal.dependsOn {preparePublication}

def getSignatureFiles = {
    def allFiles = project.tasks.signArtifacts.signatureFiles.collect { it }
    def signedSources = allFiles.find { it.name.contains('-sources') }
    def signedPom = allFiles.find { it.name.startsWith('pom-')}
    def signedAar = (allFiles - [signedSources] - [signedPom]).first()
    return [
            [archive: signedSources, classifier: 'sources', extension: 'jar.asc'],
            [archive: signedAar,     classifier: null,      extension: 'aar.asc'],
            [archive: signedPom,     classifier: null,      extension: 'pom.asc']
    ]
}

publishing {
    publications {
        androidLib(MavenPublication) {
            artifact aar
            groupId "com.microsoft.appcenter"
            artifactId "espresso-test-extension"

            artifact(sourcesJar)

            getSignatureFiles().each { signature ->
                artifact(signature.archive) {
                    classifier = signature.classifier
                    extension = signature.extension
                }
            }

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }

                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'Visual Studio App Center espresso test extensions'
                    description 'Extensions for producing nice looking test reports for espresso tests in Visual Studio App Center'
                    url 'http://appcenter.ms'
                    scm {
                        connection 'scm:git:git@github.com:xamarinhq/test-cloud-espresso-extensions.git'
                        developerConnection 'scm:git:git@github.com:xamarinhq/test-cloud-espresso-extensions.git'
                        url 'git@github.com:xamarinhq/test-cloud-espresso-extensions.git'
                    }
                    licenses {
                        license {
                            name 'MIT License'
                            url 'http://www.opensource.org/licenses/mit-license.php'
                        }
                    }
                    developers {
                        developer {
                            name 'Chris Fuentes'
                            email 'Chris.Fuentes@microsoft.com'
                            organization 'Microsoft'
                            organizationUrl 'http://www.microsoft.com'
                        }
                        developer {
                            name 'Simon SÃ¸ndergaard'
                            email 'sisonder@microsoft.com'
                            organization 'Microsoft'
                            organizationUrl 'http://www.microsoft.com'
                        }
                    }
                }
                asNode().getByName('packaging').first().setValue('aar') // force packaging to aar
            }
        }
    }
}


bintray {
    user = bintrayuser
    key = bintraykey

    pkg {
        repo = bintrayrepo
        name = 'espresso-test-extension'
        userOrg = bintrayuserorg
        websiteUrl = 'http://appcenter.ms'
        vcsUrl = 'git@github.com:xamarinhq/test-cloud-espresso-extensions.git'
        licenses = ['MIT']
    }

    publications = ['androidLib']
    publish = true
}

dependencies {
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    compile group: 'com.google.code.gson', name: 'gson', version: '2.7'
    provided 'com.android.support.test:runner:0.5'
}
