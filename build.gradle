buildscript {
    repositories {
        google()
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:3.1.1'
        }

    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.3"
    id 'digital.wup.android-maven-publish' version '3.6.2'
    id 'net.researchgate.release' version '2.6.0'
}

apply plugin: 'com.android.library'


android {
    compileSdkVersion 23

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 23
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'OldTargetApi', 'UnusedResources'
    }
}

repositories {
    mavenLocal()
    google()
    jcenter()
}

apply plugin: 'maven-publish'

task sourcesJar(type: Jar) {
    // There is no allSources in the androidPlugin :-(
    from 'src'
    archiveClassifier = 'sources'
}

publishing {
    publications {
        androidLib(MavenPublication) {
            groupId "com.microsoft.appcenter"
            artifactId "espresso-test-extension"

            from components.android
            artifact sourcesJar

            pom {
                name = 'Visual Studio App Center espresso test extensions'
                description = 'Extensions for producing nice looking test reports for espresso tests in Visual Studio App Center'
                url = 'http://appcenter.ms'
                scm {
                    connection = 'scm:git@github.com:Microsoft/AppCenter-Test-Espresso-Extensions.git'
                    developerConnection = 'scm:git@github.com:Microsoft/AppCenter-Test-Espresso-Extensions.git'
                    url = 'git@github.com:Microsoft/AppCenter-Test-Espresso-Extensions.git'
                }
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        name = 'Chris Fuentes'
                        email = 'Chris.Fuentes@microsoft.com'
                        organization  = 'Microsoft'
                        organizationUrl = 'http://www.microsoft.com'
                    }
                    developer {
                        name = 'Simon Scharf'
                        email = 'sisonder@microsoft.com'
                        organization = 'Microsoft'
                        organizationUrl = 'http://www.microsoft.com'
                    }
                }
            }
        }
    }
}

apply plugin: 'signing'

signing {
    sign publishing.publications.androidLib
}

afterReleaseBuild.dependsOn bintrayPublish

bintray {
    user = bintrayuser
    key = bintraykey

    pkg {
        repo = bintrayrepo
        name = 'espresso-test-extension'
        userOrg = bintrayuserorg
        websiteUrl = 'http://appcenter.ms'
        vcsUrl = 'git@github.com:Microsoft/AppCenter-Test-Espresso-Extensions.git'
        licenses = ['MIT']
    }

    publications = ['androidLib']
    publish = true
}

dependencies {
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testImplementation 'junit:junit:4.12'
    testImplementation 'pl.pragmatists:JUnitParams:1.1.1'

    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    compileOnly 'com.android.support.test:runner:1.0.2'
}
